# distutils: language = c++
# cython: boundscheck=False
# cython: wraparound=False
# cython: initializedcheck=False
# cython: cdivision=True
# cython: nonecheck=False
from Gates cimport Gate, Probe, Profile, CPP_Gate, hide, reveal, pop, add_profile, vector
from Store cimport get, decode
from Const cimport *
from cpython.list cimport PyList_GET_SIZE, PyList_GET_ITEM
from libcpp.unordered_map cimport unordered_map

cdef class IC:
    def __cinit__(self):
        self.id = IC_ID
        self.gate_infolist_ptr = NULL
        self.profiles_ptr = NULL

    def __init__(self, int id, str name):
        self.inputs = []
        self.internal = []
        self.outputs = []

        self.codename = name
        self.custom_name = ''
        self.code = ()
        self.map = []
        self.tag = ''
        self.description = ''
        self.pin_orientations = [[], []]
        self.gate_infolist_ptr = NULL
        self.profiles_ptr = NULL

    def __repr__(self):
        return self.codename if self.custom_name == '' else self.custom_name

    def __str__(self):
        return self.codename if self.custom_name == '' else self.custom_name

    cpdef object getcomponent(self, int choice):
        '''Get a gate from the store and register it under the right pin group'''
        cdef object gt = get(choice, self.gate_infolist_ptr[0], self.profiles_ptr[0], self.gate_verse)
        if gt:
            if gt.id == IC_INPUT_PIN_ID:
                rank = len(self.inputs)
                self.inputs.append(gt)
            elif gt.id == IC_OUTPUT_PIN_ID:
                rank = len(self.outputs)
                self.outputs.append(gt)
            else:
                rank = len(self.internal)
                self.internal.append(gt)
            gt.codename = gt.codename + '-' + str(rank)
            gt.code = (choice, rank, self.code)
        return gt

    cpdef void addgate(self, object source):
        '''Add an already-existing gate into the IC's pin groups
        this is for ic creation'''
        if source.id == IC_INPUT_PIN_ID:
            rank = len(self.inputs)
            self.inputs.append(source)
        elif source.id == IC_OUTPUT_PIN_ID:
            rank = len(self.outputs)
            self.outputs.append(source)
        else:
            rank = len(self.internal)
            self.internal.append(source)
        source.codename = source.codename + '-' + str(rank)
        source.code = (source.code[0], rank, self.code)

    cpdef void configure(self, list dictionary):
        '''Load an IC from its serialised data and wire everything up. 
        similar to generation of circuit but for ic'''
        cdef unordered_map[int,int] pseudo
        pseudo[-1] = -1
        pseudo.reserve(PyList_GET_SIZE(dictionary[MAP]))
        self.custom_name = dictionary[CUSTOM_NAME]
        self.map = dictionary[MAP]
        if len(dictionary) > TAG:
            self.tag = dictionary[TAG]
        if len(dictionary) > DESCRIPTION:
            self.description = dictionary[DESCRIPTION]
        if len(dictionary) > PIN_ORIENTATIONS:
            self.pin_orientations = dictionary[PIN_ORIENTATIONS]
        self.load_components(dictionary, pseudo) # first phase, loading to hashmap pseudo
        self.clone(pseudo) # second phase, wiring up

    cpdef void load_components(self, list dictionary, unordered_map[int,int]& pseudo):
        '''Instantiate the IC's internal gates and map their old locations to new ones
        actually the first phase of generation. 
        creates configures location of gates current vs. old location in info'''
        cdef Gate gate
        cdef list comp_code
        for comp_code in dictionary[MAP]:
            gate = self.getcomponent(comp_code[ID])
            pseudo[comp_code[LOCATION]] = gate.location

    cpdef void clone(self, unordered_map[int,int]& pseudo):
        '''Wire up each internal gate using the location map built during load
        actually the second phase of generation.
        '''
        cdef Gate gate
        for i in self.map:
            gate = <Gate>self.gate_verse[pseudo[i[LOCATION]]]
            gate.clone(i, pseudo)

    cpdef void load_to_cluster(self, list cluster):
        '''Mark all internal gates as scheduled and collect them into the cluster list
        for copy paste'''
        cdef CPP_Gate* gate_infolist = self.gate_infolist_ptr[0].data()
        cdef Gate i
        for i in self.outputs + self.inputs + self.internal:
            cluster.append(i.location)
            gate_infolist[i.location].flags |= FLAG_MARK

    cpdef list full_data(self):
        '''Serialise the IC with full connection info, used for saving the parent circuit'''
        cdef Gate i
        cdef list dictionary = [
            self.custom_name,
            IC_ID,
            self.code,
            self.tag,
            [i.full_data() for i in self.inputs + self.outputs + self.internal],
            self.description,
            self.pin_orientations,
        ]
        return dictionary

    cpdef list partial_data(self):
        '''Serialise the IC with only the connections visible from outside the cluster, used for copy/paste and IC files'''
        cdef Gate i
        cdef list dictionary = [
            self.custom_name,
            IC_ID,
            self.code,
            self.tag,
            [i.partial_data() for i in self.inputs + self.outputs + self.internal],
            self.description,
            self.pin_orientations,
        ]
        return dictionary

    cpdef void implement(self, unordered_map[int,int]& pseudo):
        '''Wire up the IC's gates into the parent circuit using the resolved location map'''
        cdef Gate gate
        cdef tuple code
        for i in self.map:
            gate = <Gate>PyList_GET_ITEM(self.gate_verse, pseudo[i[LOCATION]])
            gate.clone(i, pseudo)

    cpdef void hide(self):
        '''Cut the IC out of the live graph — disconnects output targets and drops input registrations'''
        cdef Gate pin_out, pin_in
        cdef CPP_Gate* pin_out_info
        cdef CPP_Gate* src_info
        cdef int index, p_idx
        cdef CPP_Gate* gate_infolist = self.gate_infolist_ptr[0].data()
        cdef Profile* p_base = self.profiles_ptr[0].data() if self.profiles_ptr != NULL and self.profiles_ptr[0].size() > 0 else NULL

        # Disconnect outputs from external targets
        if p_base != NULL:
            for pin_out in self.outputs:
                pin_out_info = &gate_infolist[pin_out.location]
                p_idx = pin_out_info.hitlist
                while p_idx != -1:
                    hide(p_idx, self.profiles_ptr[0], gate_infolist, self.gate_verse)
                    p_idx = p_base[p_idx].next

        # Disconnect inputs from external sources
        for pin_in in self.inputs:
            for index, source_loc in enumerate(<list>pin_in._sources):
                if source_loc != -1:
                    src_info = &gate_infolist[source_loc]
                    pop(src_info.hitlist, src_info.hitlist_count, self.profiles_ptr[0], gate_infolist, &gate_infolist[pin_in.location], index)

    cpdef void reveal(self):
        '''Plug the IC back into the live graph — re-registers inputs and reconnects output targets'''
        cdef Gate pin_in, pin_out
        cdef CPP_Gate* pin_in_info
        cdef CPP_Gate* pin_out_info
        cdef CPP_Gate* src_info
        cdef int source_loc, p_idx
        cdef CPP_Gate* gate_infolist = self.gate_infolist_ptr[0].data()
        cdef Profile* p_base = self.profiles_ptr[0].data() if self.profiles_ptr != NULL and self.profiles_ptr[0].size() > 0 else NULL

        # Re-register in external source hitlists
        for pin_in in self.inputs:
            pin_in_info = &gate_infolist[pin_in.location]
            source_loc = pin_in._sources[0]
            if source_loc != -1:
                src_info = &gate_infolist[source_loc]
                add_profile(src_info.hitlist, src_info.hitlist_count, self.profiles_ptr[0], &gate_infolist[pin_in.location], 0, src_info.output)
                pin_in_info.logic += (src_info.output == pin_in_info.seed)
            pin_in.process()

        # Reconnect output targets via hitlist
        p_base = self.profiles_ptr[0].data() if self.profiles_ptr != NULL and self.profiles_ptr[0].size() > 0 else NULL
        if p_base != NULL:
            for pin_out in self.outputs:
                pin_out_info = &gate_infolist[pin_out.location]
                p_idx = pin_out_info.hitlist
                while p_idx != -1:
                    reveal(p_idx, self.profiles_ptr[0], pin_out, self.gate_verse)
                    p_idx = p_base[p_idx].next

    cpdef void reset(self):
        '''Reset all internal gates back to unknown state'''
        cdef Gate g
        for i in self.inputs + self.internal + self.outputs:
            if i.id != IC_ID:
                g = <Gate>i
                g.reset()
            else:
                (<IC>i).reset()

    cpdef void showinputpins(self):
        '''Print the IC's input pins with their index'''
        for i, gate in enumerate(self.inputs):
            print(f'{i}. {gate}')

    cpdef void showoutputpins(self):
        '''Print the IC's output pins with their index'''
        for i, gate in enumerate(self.outputs):
            print(f'{i}. {gate}')

    cpdef void info(self):
        '''Print the IC's inputs, internals, and outputs with their connections'''
        cdef Gate pin
        cdef CPP_Gate* pin_info
        cdef int p_idx
        cdef list gate_verse = self.gate_verse
        cdef Profile* p_base = self.profiles_ptr[0].data() if self.profiles_ptr != NULL and self.profiles_ptr[0].size() > 0 else NULL
        print(f"\n  IC: {self.codename} (Code: {self.code})")
        print("  " + "-" * 40)
        cdef CPP_Gate* gate_infolist = self.gate_infolist_ptr[0].data()
        if self.inputs:
            print("  INPUTS:")
            for pin in self.inputs:
                targets = []
                pin_info = &gate_infolist[pin.location]
                p_idx = pin_info.hitlist
                if p_base != NULL:
                    while p_idx != -1:
                        if p_base[p_idx].target != NULL:
                            targets.append(str(<Gate>PyList_GET_ITEM(gate_verse, p_base[p_idx].target - gate_infolist)))
                        p_idx = p_base[p_idx].next
                print(f"    {pin.codename}: out={pin.getoutput()}, to={', '.join(targets) if targets else 'None'}")

        if self.internal:
            print("  INTERNAL:")
            for pin in self.internal:
                if isinstance(pin.sources, list):
                    ch = [f"[{i}]:{c}" for i, c in enumerate(pin.sources) if c != -1]
                    ch_str = ", ".join(ch) if ch else "None"
                else:
                    ch_str = f"val:{pin.sources}"
                tgt = []
                pin_info = &gate_infolist[pin.location]
                p_idx = pin_info.hitlist
                if p_base != NULL:
                    while p_idx != -1:
                        if p_base[p_idx].target != NULL:
                            tgt.append(str(<Gate>PyList_GET_ITEM(gate_verse, p_base[p_idx].target - gate_infolist)))
                        p_idx = p_base[p_idx].next
                tgt_str = ", ".join(tgt) if tgt else "None"
                print(f"    {pin.codename}: out={pin.getoutput()}, sources={ch_str}, targets={tgt_str}")

        if self.outputs:
            print("  OUTPUTS:")
            for pin in self.outputs:
                if isinstance(pin.sources, list):
                    ch = [f"{c}" for c in pin.sources if c != -1]
                    ch_str = ", ".join(ch) if ch else "None"
                else:
                    ch_str = "None"
                print(f"    {pin.codename}: out={pin.getoutput()}, from={ch_str}")

        print("  " + "-" * 40)
